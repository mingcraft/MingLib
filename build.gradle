plugins {
    id 'java'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '7.0.0'
    id("co.uzzu.dotenv.gradle") version "1.2.0"
    id 'application'
}

group = "com.mingcraft.minglib"
version = "1.0.6"

mainClassName = 'MingLib'

compileJava.options.encoding = 'UTF-8'
tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

repositories {
    mavenCentral()
    mavenLocal()
    maven {
        name = 'papermc-repo'
        url = 'https://papermc.io/repo/repository/maven-public/'
    }
}

dependencies {
    compileOnly 'io.papermc.paper:paper-api:1.17.1-R0.1-SNAPSHOT'
    compileOnly 'org.spigotmc:spigot:1.17.1-R0.1-SNAPSHOT'

    implementation 'org.mongodb:mongodb-driver-sync:4.3.2'
    implementation 'org.junit.jupiter:junit-jupiter:5.8.1'

    implementation 'com.google.api-client:google-api-client:1.32.1'
    implementation 'com.google.oauth-client:google-oauth-client-jetty:1.32.1'
    implementation 'com.google.apis:google-api-services-drive:v3-rev20210919-1.32.1'
    implementation 'com.google.http-client:google-http-client-jackson2:1.40.0'
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

task replaceVersionInREADME {
    ant.replaceregexp(match:'com\\.github.mingcraft\\:minglib\\:([0-9\\.]+)', replace:"com.github.mingcraft:minglib:${version}", flags:'g', byline:true) {
        fileset(dir: '.', includes: 'README.md')
    }
}

test {
    useJUnitPlatform()
}

jar {
    destinationDirectory = file(env.JAR_DIR.value)
}

shadowJar {
    manifest {
        attributes 'Main-Class': 'com.mingcraft.minglib.MingLib'
    }
    destinationDirectory = file(env.JAR_DIR.value)
}

allprojects {
    repositories {
        maven { url "https://jitpack.io" }
    }
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/mingcraft/minglib")
            credentials {
                username = env.GPR_USER.value
                password = env.GPR_KEY.value
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            groupId = "com.mingcraft"
            artifactId = "minglib"
            version = this.version
            from(components.java)
        }
    }
}
